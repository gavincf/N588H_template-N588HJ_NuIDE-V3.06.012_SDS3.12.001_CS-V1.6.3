;=======================================
;	<< BK0S0.ASM >>
;===========================================
;

.IF ( C_SCORE_2_4DIGIT_EN .EQ.1)
;===========================
; max: 9999
;===========================

BK0_SCORE_2_4DIGIT:
	
	LDA	R_P1_SCORE_NO1
	STA	REG_HL
	LDA	R_P1_SCORE_NO
	STA	REG_EA

BK0_SCORE_2_4DIGIT_S000:
	LDA	#00H
	STA	R_DIGIT_3
	STA	R_DIGIT_2
	STA	R_DIGIT_1

SCORE_2_4DIGIT_S010:
	LDA	REG_HL
	CMP	#.HIGH.1000
	BCC	SCORE_2_4DIGIT_S100
	BNE	SCORE_2_4DIGIT_S011
	
	LDA	REG_EA
	CMP	#.LOW.1000
	BCC	SCORE_2_4DIGIT_S100

SCORE_2_4DIGIT_S011:
	SEC
	LDA	REG_EA
	SBC	#.LOW.1000
	STA	REG_EA
	LDA	REG_HL
	SBC	#.HIGH.1000
	STA	REG_HL
	
	INC	R_DIGIT_3
	BRA	SCORE_2_4DIGIT_S010
	
SCORE_2_4DIGIT_S100:

	LDA	REG_HL
	BNE	SCORE_2_4DIGIT_S111
	
	LDA	REG_EA
	CMP	#100
	BCC	SCORE_2_4DIGIT_S200

SCORE_2_4DIGIT_S111:
	SEC	
	LDA	REG_EA
	SBC	#100
	STA	REG_EA
	LDA	REG_HL
	SBC	#0
	STA	REG_HL
	
	INC	R_DIGIT_2
	BRA	SCORE_2_4DIGIT_S100
	
SCORE_2_4DIGIT_S200:
	LDA	REG_EA
	CMP	#10
	BCC	SCORE_2_4DIGIT_S300
	
	SEC	
	LDA	REG_EA
	SBC	#10
	STA	REG_EA

	INC	R_DIGIT_1
	BRA	SCORE_2_4DIGIT_S200
	
SCORE_2_4DIGIT_S300:
	LDA	REG_EA
	STA	R_DIGIT_0


SCORE_2_4DIGIT_S400:
	
	RTS
.ENDIF

.IF ( C_SCORE_2_3DIGIT_EN .EQ.1)
;=============================
; MAX: 999
;=============================
BK0_SCORE_2_3DIGIT:
	
	LDA	R_P1_SCORE_NO1
	STA	REG_HL
	LDA	R_P1_SCORE_NO
	STA	REG_EA

BK0_SCORE_2_3DIGIT_S000:

	LDA	#00H
	STA	R_DIGIT_2
	STA	R_DIGIT_1

SCORE_2_3DIGIT_S010:
	LDA	REG_HL
	BNE	SCORE_2_3DIGIT_S011
	
	LDA	REG_EA
	CMP	#100
	BCC	SCORE_2_3DIGIT_S100

SCORE_2_3DIGIT_S011:
	SEC	
	LDA	REG_EA
	SBC	#100
	STA	REG_EA
	LDA	REG_HL
	SBC	#0
	STA	REG_HL
	
	INC	R_DIGIT_2
	BRA	SCORE_2_3DIGIT_S010
	
SCORE_2_3DIGIT_S100:
	LDA	REG_EA
	CMP	#10
	BCC	SCORE_2_3DIGIT_S200
	
	SEC	
	LDA	REG_EA
	SBC	#10
	STA	REG_EA

	INC	R_DIGIT_1
	BRA	SCORE_2_3DIGIT_S100
	
SCORE_2_3DIGIT_S200:
	LDA	REG_EA
	STA	R_DIGIT_0


;SCORE_2_3DIGIT_S300:
	
	RTS
.ENDIF
