;===================================================
;
;===================================================

BK0_IRRX_STOP:
	
	JMP	IRRX_CHK_LOW_ERROR

BK0_DET_IRRX_ING:
	LDA	IRRX_PT
	BNE	BK0_DET_IRRX_ING_S10
	LDA	#0
	RTS
BK0_DET_IRRX_ING_S10:
	LDA	#1
	RTS
	
IRRX_TMG0_ISR:
	LDA	IRRX_DLYCNT
	CMP	#C_IRRX_DLYCNT_MAX
	BCS	IRRX_TMG0_ISR_S10
	INC	IRRX_DLYCNT
	RTS

IRRX_TMG0_ISR_S10:
	LDA	#0
	STA	IRRX_PT

	RTS
	
;; ===========================  NEC_V3
;简化头码
;T = 0.625MS / unit
;RX PART
;；LOW： carrier
;头：8T HIGH
;0：1T LOW, 1T HIGH，
;1：3T LOW, 1T HIGH，
;有校验位码
;;-------------------------------
;;NEC_V3              
;;简化头码            
;;T = 0.625MS / unit  
;;TX PART
;;；HIGH： carrier     
;;头：8T HIGH
;;0：1T LOW, 1T HIGH，
;;1：3T LOW, 1T HIGH，
;;有校验位码
; --------- DATA 从高到低： 方便接收

;=========================
;MAIN LOOP 程序
;
;=========================
IRRX_CHK_LOW_END3:
	
	RTS

IRRX_LOOP_CHK:
;
	MR_DET_IR_PIN
	BNE	IRRX_LOOP_CHK_HIGH
;LOW
	MR_DET_IR_PIN_ST
	BEQ	IRRX_CHK_LOW_END3
;LOW
	MR_CLR_IR_PIN_ST
;
;	LDA	#C_BIT7
;	BIT	IRRX_FLAG
	MR_DET_IRRX_REC_FLAG
	BNE	IRRX_CHK_LOW_END3 ;received flag set

	LDA	IRRX_PT
	BEQ	IRRX_CHK_LOW_HEAD
	CMP	#1
	BEQ	IRRX_CHK_LOW_END3
	
	BRA	IRRX_CHK_LOW_S10
	
IRRX_CHK_LOW_HEAD:
;HEAD
	JSR	BK0_START_TMG0_0250MS
	LDA	#0
	STA	IRRX_DLYCNT

	INC	IRRX_PT
	RTS

IRRX_CHK_LOW_S10:
;DATA
; 250US / unit
;DATA0:1*0.625=0.625; 1-2
;DATA1:3*0.625=1.875; 3-4
	LDA	IRRX_DLYCNT
	CMP	#C_DATA0_MIN
	BCC	IRRX_CHK_LOW_ERROR
	CMP	#C_DATA0_MAX
	BCS	IRRX_CHK_LOW_S20
;DATA0

	JMP	IRRX_CHK_LOW_S23

IRRX_CHK_LOW_S20:
	CMP	#C_DATA1_MAX
	BCS	IRRX_CHK_LOW_ERROR
	CMP	#C_DATA1_MIN
	BCC	IRRX_CHK_LOW_ERROR
;DATA1
	
IRRX_CHK_LOW_S23:
	ROL	IRRX_DATA
	
	LDA	IRRX_PT
	CMP	#C_BIT_MAX
	BCS	IRRX_CHK_H_FINISH

	INC	IRRX_PT
	JMP	IRRX_CHK_LOW_END2
IRRX_CHK_H_FINISH:
;checksum

	LDA	IRRX_DATA
	AND	#0FH
	STA	REG_EA
;
	LDA	IRRX_DATA
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	EOR	#0FH
	CMP	REG_EA
	BNE	IRRX_CHK_LOW_ERROR
;
; set received flag
	MR_IRRX_SET_REC_FLAG

;============================
;		
;=================================
	JMP	IRRX_RECEIVED_SOLVE
IRRX_RECEIVED_SOLVE_RET:
	
;=============================
IRRX_CHK_LOW_ERROR:
	LDA	#0
	STA	IRRX_PT
	
IRRX_CHK_LOW_END2:
	RTS
IRRX_LOOP_CHK_HIGH:
	MR_DET_IR_PIN_ST
	BNE	IRRX_CHK_LOW_END2
	MR_SET_IR_PIN_ST

;	LDA	#C_BIT7
;	BIT	IRRX_FLAG
	MR_DET_IRRX_REC_FLAG
	BNE	IRRX_CHK_LOW_END2 ;received flag set
;
	LDA	IRRX_PT
	BEQ	IRRX_LOOP_CHK_END
	CMP	#1
	BNE	IRRX_LOOP_CHK_HIGH_S10
;HEAD
	LDA	IRRX_DLYCNT
	CMP	#C_HEAD_MIN
	BCC	IRRX_CHK_LOW_ERROR
	CMP	#C_HEAD_MAX
	BCS	IRRX_CHK_LOW_ERROR
;head 
	INC	IRRX_PT
	
IRRX_LOOP_CHK_HIGH_S10:
	LDA	#0
	STA	IRRX_DLYCNT
	JSR	BK0_START_TMG0_0250MS
	
IRRX_LOOP_CHK_END:
	RTS
	
	
	
	