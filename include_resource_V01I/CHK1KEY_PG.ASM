




;-------------------------
;;------------------- POI
;-------------------------

;=================================
.IF (C_CHK1KEY_EN1 .EQ. 1) .OR. (C_CHK1KEY_EN2 .EQ. 1)
;============================
;只有一个键有效;多键按下看哪个键先扫到。
;
;============================

BK0_CHK1KEY_HOLD:
	
	JSR	SCAN_LINE1
	LDA	<R_CHK1KEY_PRESS
	STA	<R_CHK1KEY_PREPRESS
	
	LDA	#.HIGH.C_CHK1KEY_LONG_MAX
	STA	<R_CHK1KEY_DLYCNT0+1
	LDA	#.LOW.C_CHK1KEY_LONG_MAX
	STA	<R_CHK1KEY_DLYCNT0
	RTS

;===============================
BK0_CHK1KEY_INIT:

	LDA	#C_NOP
	STA	<R_CHK1KEY_PREPRESS
	STA	<R_CHK1KEY_PRESS

	RTS

BK0_DET_CHK1KEY: ; for auto sleep

	LDA	<R_CHK1KEY_DLYCNT0+1
	ORA	<R_CHK1KEY_DLYCNT0
	BEQ	BK0_DET_CHK1KEY_END; NO KEY PRESS/HOLD

	LDA	<R_CHK1KEY_DLYCNT0+1
	CMP	#.HIGH.C_CHK1KEY_LONG_MAX
	BCC	BK0_DET_CHK1KEY_S10
	BNE	BK0_DET_CHK1KEY_END; KEY HOLD
	LDA	<R_CHK1KEY_DLYCNT0
	CMP	#.LOW.C_CHK1KEY_LONG_MAX
	BCS	BK0_DET_CHK1KEY_END; KEY HOLD
	
BK0_DET_CHK1KEY_S10:;BUSY
	LDA	#1
	RTS
BK0_DET_CHK1KEY_END:;FREE
	LDA	#0
	RTS
.ENDIF
	
;=================================
; 第1种扫描单个或者组合键方式
;=================================

.IF (C_CHK1KEY_EN1 .EQ. 1)
CHK1KEY:
	LDA	<R_CHK1KEY_DEB
	BEQ	CHK1KEY_S1000
	LDA	<R_CHK1KEY_PREPRESS
	CMP	#C_NOP
	BEQ	CHK1KEY_S1000
;// loop check the new key
	DEC	<R_CHK1KEY_DEB
	LDA	<R_CHK1KEY_PRESS
	CMP	#C_NOP
	BNE	CHK1KEY_S0100
	LDA	#C_NOP
	STA	<R_CHK1KEY_PREPRESS
	
	BRA	CHK1KEY_S1000
CHK1KEY_S0100:
	CMP	<R_CHK1KEY_PREPRESS
	BEQ	CHK1KEY_END2
;NOISE
;KEY NEW START DEBOUNCE
	
	JMP	CHK1KEY_S1001
CHK1KEY_END2:
	RTS 
	
CHK1KEY_S1000:
	LDA	<R_CHK1KEY_PREPRESS
	CMP	#C_NOP
	BNE	CHK1KEY_S2000
;pre empty; check new key
	LDA	<R_CHK1KEY_PRESS
	CMP	#C_NOP
	BEQ	CHK1KEY_S1500 ; 没有按键.
	
CHK1KEY_S1001: ; new key start
	LDA	<R_CHK1KEY_PRESS
	STA	<R_CHK1KEY_PREPRESS
	LDA	#1
	STA	<R_CHK1KEY_DLYCNT0
	LDA	#C_CHK1KEY_DEB-1  ; debounce
	STA	<R_CHK1KEY_DEB

	RTS
CHK1KEY_S1500: ; noise
	
	JMP	CHK1KEY_RELEASE;EMPTY

	
CHK1KEY_S2000:
;//Pre is not empty: check the pressed key
	LDA	<R_CHK1KEY_PREPRESS
	AND	#C_CHK1KEY_V_MASK
	CMP	<R_CHK1KEY_PRESS
	BNE	CHK1KEY_S2400 ; 与之前按键不同:as new key
;
	LDA	<R_CHK1KEY_DLYCNT0
	CMP	#C_CHK1KEY_LONG0_V0
	BCS	CHK1KEY_END1
	INC	<R_CHK1KEY_DLYCNT0
	
	LDA	<R_CHK1KEY_DLYCNT0
	CMP	#C_CHK1KEY_DEB
	BNE	CHK1KEY_S2010
;SHORT KEY PRESS
	LDA	<R_CHK1KEY_PREPRESS
	STA	WB_KEYCH_CODE+1
	
	JMP	CHK1KEY_SHORT_F

	
CHK1KEY_S2010:
	LDA	<R_CHK1KEY_DLYCNT0
	CMP	#C_CHK1KEY_LONG0_V0
	BCC	CHK1KEY_END1
;LONG KEY PRESS
;	LDA	<R_CHK1KEY_PREPRESS
;	AND	#7FH


	JMP	CHK1KEY_LONG_F

CHK1KEY_END1:; time out
	RTS

CHK1KEY_S2400: ; 与之前按键不同.
	LDA	<R_CHK1KEY_DLYCNT0
	CMP	#C_CHK1KEY_DEB
	BCC	CHK1KEY_RELEASE
;

	LDA	<R_CHK1KEY_PRESS
	CMP	#C_NOP
	BNE	CHK1KEY_S2700
;key release
	LDA	<R_CHK1KEY_PREPRESS
	AND	#C_KEY_HOLD
	BEQ	CHK1KEY_S2600
;HOLD KEY release
	
	JMP	CHK1KEY_LONG_R
CHK1KEY_LONG_R_RET:
	BRA	CHK1KEY_RELEASE
	
CHK1KEY_S2600:
;short key release	
	
	JMP	CHK1KEY_SHORT_R
CHK1KEY_SHORT_R_RET:
	BRA	CHK1KEY_RELEASE
	
CHK1KEY_S2700:
	RTS

CHK1KEY_RELEASE:
	LDA	#C_NOP
	STA	<R_CHK1KEY_PREPRESS
	STZ	<R_CHK1KEY_DEB
	STZ	<R_CHK1KEY_DLYCNT0
	RTS
	
.ENDIF

	

