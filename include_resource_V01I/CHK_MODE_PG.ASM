

;检测方式，多个PIN，只有到只一个PIN是有效位时才修改模式值，否则保留上一模式值
;
BK0_CHK_MODE:
;扫描两次
	LDA	R_MODE_FG
	AND	#C_BIT4
	BNE	BK0_CHK_MODE_2ND
;1st time
	JSR	BK0_CHK_MODE_LINE
	LDA	R_MODE_CHK
	AND	#0FH
	CMP	#C_MODE_NOP ;---------------------V02
	BEQ	BK0_CHK_MODE_NOISE; noise
	STA	REG_EA
;
	LDA	R_MODE_FG
	AND	#0FH
	CMP	REG_EA
	BEQ	BK0_CHK_MODE_END;same with current mode ,have be solved.
;
;diferrent from current mode
;
	LDA	#C_BIT4
	TSB	R_MODE_FG
;存储
	ASL	R_MODE_CHK
	ASL	R_MODE_CHK
	ASL	R_MODE_CHK
	ASL	R_MODE_CHK
;
	LDA	#C_CHKMODE_DEB
	STA	R_CHKMODE_DEB; <key debounce
;
	BRA	BK0_CHK_MODE_END
	
BK0_CHK_MODE_2ND:
	INC	R_CHKMODE_DEB
	BNE	BK0_CHK_MODE_END
	
	JSR	BK0_CHK_MODE_LINE
	LDA	R_MODE_CHK
	AND	#0FH
	STA	REG_EA
	
	LDA	R_MODE_CHK
	LSR	A
	LSR	A
	LSR	A
	LSR	A
	CMP	REG_EA
	BNE	BK0_CHK_MODE_NOISE
;两次一样。
;set flag

	LDA	R_MODE_FG
	AND	#C_BIT4V	; reset 1st time
	STA	R_MODE_FG
;

	JMP	BK0_CHK_MODE_CHG
BK0_CHK_MODE_CHG_RETURN:
	RTS
BK0_CHK_MODE_NOISE:
	LDA	R_MODE_FG
	AND	#C_BIT4V	; reset 1st time
	STA	R_MODE_FG
	
BK0_CHK_MODE_END:

	RTS



BK0_CHK_MODE_LINE:
;assume empty mode
	LDA	R_MODE_CHK
	AND	#0F0H
	ORA	#C_MODE_NOP
	STA	R_MODE_CHK
;0 - (C_MAX_MODE -1 )
	
	LDA	!SR_CHKM0_PORT
	AND	#C_CHKM0_BIT
	BNE	BK0_CHK_MODE_S10
	LDA	R_MODE_CHK
	AND	#0F0H
	ORA	#C_M0_V	; essume
	STA	R_MODE_CHK
	RTS
BK0_CHK_MODE_S10:
	.IF ( C_MAX_MODE .GT. 1 )
	LDA	!SR_CHKM1_PORT
	AND	#C_CHKM1_BIT
	BNE	BK0_CHK_MODE_S20
	LDA	R_MODE_CHK
	AND	#0F0H
	ORA	#C_M1_V	; essume
	STA	R_MODE_CHK
	RTS
BK0_CHK_MODE_S20:
	.ENDIF
	.IF ( C_MAX_MODE .GT. 2 )
	LDA	!SR_CHKM2_PORT
	AND	#C_CHKM2_BIT
	BNE	BK0_CHK_MODE_S30
	LDA	R_MODE_CHK
	AND	#0F0H
	ORA	#C_M2_V	; essume
	STA	R_MODE_CHK
	RTS
BK0_CHK_MODE_S30:
	.ENDIF
	.IF ( C_MAX_MODE .GT. 3 )
	LDA	!SR_CHKM3_PORT
	AND	#C_CHKM3_BIT
	BNE	BK0_CHK_MODE_S40
	LDA	R_MODE_CHK
	AND	#0F0H
	ORA	#C_M3_V	; essume
	STA	R_MODE_CHK
BK0_CHK_MODE_S40:
	.ENDIF
	RTS








